private function init_challenges() {
    if (!defined(std_alpha)) {
        challenge stage(2) std_alpha;
    }
    if (!defined(std_beta)) {
        challenge stage(2) std_beta;
    }
}
/*
function multiset_assumes(int opid, expr cols[]) {
    piop_update(0, 0, opid, 1, cols);
}

function multiset_proves(int opid, expr cols[]) {
    piop_update(1, 0, opid, 1, cols);
}
*/
function multiset_assumes(int opid, expr sel, expr __cols[]) {
    piop_update(0, 0, opid, sel, __cols);
}

function multiset_proves(int opid, expr sel, expr cols[]) {
    piop_update(1, 0, opid, sel, cols);
}

/*
function lookup_assumes(int opid, expr cols[]) {
    piop_update(0, 1, opid, 1, cols);
}

function lookup_proves(int opid, expr cols[]) {
    piop_update(1, 1, opid, 1, cols);
}
*/

// Right now, we assumes that the user proves the multiplicity counter.
// However, we could let the user to only give us the selector and the columns, and we compute the multiplicity counter.
function lookup_assumes(int opid, expr mul_count, expr cols[]) {
    piop_update(0, 1, opid, mul_count, cols);
}

function lookup_proves(int opid, expr mul_count, expr cols[]) {
    piop_update(1, 1, opid, mul_count, cols);
}

/*
// To support vector range checks, we would need to compute a table of all permutations of the range [min,max-1].
function range_check(int opid, expr cols[], int min, int max) {
    for (int i = 0; i < length(cols); i = i + 1) {
        piop_update(0, 1, opid, 1, cols[i]);
    }

    col fixed RANGE = [min,min+1..+..max-1]; // QUESTION: Does this still work if max - min > N?
    // col witness mul_count = compute_multiplicities(cols, RANGE);
    piop_update(1, 1, opid, mul_count, RANGE);
}
*/

function range_check(int opid, expr sels[], expr cols[], int min, int max) {
    for (int i = 0; i < length(cols); i = i + 1) {
        piop_update(0, 1, opid, sels[i], cols[i]);
    }

    col fixed RANGE = [min,min+1..+..max-1];
    // col witness mul_count = compute_multiplicities(cols, RANGE);
    piop_update(1, 1, opid, mul_count, RANGE);
}

// QUESTION: Can the following O(n³) algorithm be implemented faster?
/**
 * Given columns C₀,...,Cₙ₋₁, and a range, computes the multiplicity counter:
        · mul_count[i] = Σⱼ |{k : Cⱼ[k] = range[i]}|
 * @param cols
 * @param range
 */
private function compute_multiplicities(expr cols[], expr range) {
    expr mul_count[length(range)] = 0; // [0,0,...,0]
    for (int i = 0; i < length(range); i = i + 1) {
        for (int j = 0; j < length(cols); j = j + 1) {
            for (int k = 0; k < length(cols[j]); k = k + 1) {
                if (cols[j][k] == range[i]) {
                    mul_count[i] = mul_count[i] + 1;
                }
            }
        }
    }
    // Note: We do not need to normalize the result, since a range has no repeated elements.
    return mul_count;
}

/**
 * Verifies the number of columns of same PIOP check (assumes, proves) is the same.
 * @param opid (unique) identifier of the PIOP check
 * @param cols_count number of columns of the PIOP check
 * @param proves 1 if proves, 0 if assumes
 */
private function check_opid_and_cols(int proves, int type, int opid, int cols_count) {

    if (cols_count < 1) {
        error(`The number of columns of PIOP #${opid} must be at least 1`);
    }

    container proof.std.piop alias piop {
        // FIX: dynamic arrays not ready
        int opids_count = 0;
        int opids[100];
        expr gsum = 0;  // Resulting sum of every subproof and every air.
                        // Declared as expression (unknown at runtime), but used as field element.
    }

    container subproof.std.piop {
        subproofval aggregate(sum) gsum_result;
    }

    container air.std.piop {
        // FIX: dynamic arrays not ready
        int gsum_assumes_count = 0;
        expr gsum_assumes_sel[100];
        expr gsum_assumes[100];

        int gsum_proves_count = 0;
        expr gsum_proves_type[100];
        expr gsum_proves_sel[100];
        expr gsum_proves[100];
    }

    container proof.std.piop.`id${opid}` alias piopid {
        int cols;
        int proves = 0;
        int assumes = 0;
    }

    if (piopid.cols == 0) {
        // first time called
        piopid.cols = cols_count;
        // add opid on a list to verify at final
        piop.opids[piop.opids_count] = opid;
        piop.opids_count = piop.opids_count + 1;
    } else if (piopid.cols != cols_count) {
        error(`The number of columns of PIOP #${opid} must be ${piopid.cols} but was ${cols_count}`);
    }

    if (proves) {
        piopid.proves = piopid.proves + 1;
    } else {
        piopid.assumes = piopid.assumes + 1;
    }
}

/**
 * Given columns C₁,...,Cₙ, reduces the vector PIOP to a single column PIOP.
 * @param opid (unique) identifier of the PIOP
 * @param cols columns of the PIOP
 * @return [opid + C₁·α + C₂·α² + ... + Cₙ·αⁿ] + β
 */
private function compress_cols(int opid, expr cols[]): expr {
    expr cols_compressed = 0;
    for (int icol = length(cols) - 1; icol >= 0; icol = icol - 1) {
        cols_compressed = (cols_compressed + cols[icol]) * std_alpha;
    }

    // the opid is added to avoid soundness issues between different PIOP checks
    cols_compressed = (cols_compressed + opid) + std_beta;
    return cols_compressed;
}

/**
 * Given columns C₀,...,Cₙ₋₁, and multiplicity counter M, it 1] defines the constraints at the air level, 2] updates the values at the subproof level, and 3] updates the constraints at the proof level.
 * @param proves boolean indicating if updating a proves or a assumes
 * @param type if 1, it is a lookup or a range check; if 0, it is a multiset
 * @param opid (unique) identifier of the PIOP
 * @param sel selector of the PIOP
 * @param cols columns of the PIOP
 */
private function piop_update(int proves, int type, int opid, expr sel, expr cols[]) {
    // verify if correct opid and cols
    check_opid_and_cols(proves, type, opid, length(cols));

    init_challenges();

    // selected vector to simple column reduction
    expr cols_compressed = compress_cols(opid, cols);

    use air.std.piop;
    if (proves) {
        // adding all sums of proves called in this air
        gsum_proves_type[gsum_proves_count] = type;
        gsum_proves_sel[gsum_proves_count] = sel;
        gsum_proves[gsum_proves_count] = cols_compressed;
        gsum_proves_count = gsum_proves_count + 1;
    } else {
        // adding all sums of assumes called in this air
        gsum_assumes_sel[gsum_assumes_count] = sel;
        gsum_assumes[gsum_assumes_count] = cols_compressed;
        gsum_assumes_count = gsum_assumes_count + 1;
    }

    // QUESTION: Should the user proves the multiplicity counter? Or we should compute it?
    // 0] computes the multiplicity counter for lookups and range checks
    // on final air compute_multiplicities_lookup_range();

    // 1] look for repeated proves/assumes's in both lookups and range checks and, if found, optimize
    // on final air lookup_range_proves_batch();

    // 2] define constraints at the air level
    on final air PIOP_air();

    // 3] update values at the subproof level
    on final subproof PIOP_subproof();

    // 4] update constraints at the proof level
    on final proof PIOP_proof();
}
/*
// QUESTION: Can the following O(n²) algorithm be implemented faster?
// Given the proves, it "merges" the repeated ones through their associated multiplicity counters.
private function lookup_range_proves_batch() {
    use air.std.piop;

    // int len = length(gsum_proves);
    int len = gsum_proves_count;

    for (int i = 0; i < len; i = i + 1) {
        for (int j = 0; j < len; j = j + 1) {
            if (i == j) continue;
            // It two proves are the same, add their multiplicity counters to one and remove the other
            if (gsum_proves[j] == gsum_proves[i] && gsum_proves_type[j] == 1) {
                gsum_proves_sel[i] = gsum_proves_sel[i] + gsum_proves_sel[j];

                gsum_proves[j] = gsum_proves[len-1];
                gsum_proves_sel[j] = gsum_proves_sel[len-1];
                gsum_proves.pop(); // QUESTION: Will array pop be implemented?
                gsum_proves_sel.pop();
            }
        }
    }
}
*/

private function PIOP_air() {
    use air.std.piop;
    
    col witness stage(2) gsum;
    
    //                                mt1       mt2       mt3       mf1       mf2       mf3
    //  gsum === 'gsum * (1 - L1) + ------- + ------- + ------- - ------- - ------- - -------
    //                                t1        t2        t3        f1        f2        f3

    col fixed L1 = [1,0...];

    expr LHS = 1;
    expr RHS1 = 0;
    // for (int i = 0; i < length(gsum_proves); i = i + 1) {
    for (int i = 0; i < gsum_proves_count; i = i + 1) {
        LHS = LHS * gsum_proves[i];

        expr tmp = gsum_proves_sel[i];
        // for (int j = 0; j < length(gsum_proves); j = j + 1) {
        for (int j = 0; j < gsum_proves_count; j = j + 1) {
            if (j != i) tmp = tmp * gsum_proves[j];
        }
        RHS1 = RHS1 + tmp;
    }

    expr RHS2a = LHS;
    expr RHS2b = 0;
    // for (int i = 0; i < length(gsum_assumes); i = i + 1) {
    for (int i = 0; i < gsum_assumes_count; i = i + 1) {
        LHS = LHS * gsum_assumes[i];
        RHS1 = RHS1 * gsum_assumes[i];
    
          println(`i:${i}`);
        expr tmp = gsum_assumes_sel[i];
        // for (int j = 0; j < length(gsum_assumes); j = j + 1) {
        for (int j = 0; j < gsum_assumes_count; j = j + 1) {
            if (j != i) tmp = tmp * gsum_assumes[j];
            println(`j:${j}`);
        }
        RHS2b = RHS2b + tmp;
    }

    expr RHS2 = RHS2a * RHS2b;
    expr RHS = RHS1 - RHS2;

    (gsum - 'gsum * (1 - L1)) * LHS === RHS;
    L1' * (gsum - subproof.std.piop.gsum_result) === 0;
}

private function PIOP_subproof() {
    proof.std.piop.gsum = proof.std.piop.gsum + subproof.std.piop.gsum_result;
}

private function PIOP_proof() {
    check_was_completed();

    proof.std.piop.gsum === 0;
}

private function check_was_completed() {
/*
    for (int opid in proof.std.piop.opids) {
        use proof.std.piop.`id${opid}`;
        if (assumes == 0) {
            error(`PIOP #${opid} defined without assumes`);
        }
        if (proves == 0) {
            error(`PIOP #${opid} defined without proves`);
        }
    }*/

    for (int index = 0; index < proof.std.piop.opids_count; index = index + 1) {
        int opid = proof.std.piop.opids[index];
        use proof.std.piop.`id${opid}`;
        if (assumes == 0) {
            error(`PIOP #${opid} defined without require`);
        }
        if (proves == 0) {
            error(`PIOP #${opid} defined without provide`);
        }
    }
}